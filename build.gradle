buildscript {
    apply plugin: 'java'
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id "com.google.protobuf" version "0.6.1"
}

repositories {
    mavenCentral()
}

configurations { // Needs to go before dependencies
    quasar
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.3'
    compile 'org.apache.logging.log4j:log4j-core:2.3'
    compile 'com.google.protobuf:protobuf-java:2.6.1'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-cli:commons-cli:1.3.1'

    compile 'co.paralleluniverse:quasar-core:0.7.2:jdk8'
    quasar  "co.paralleluniverse:quasar-core:0.7.2:jdk8@jar"
}

classes {
    doFirst {
        println "Excuting scanSuspendables..."
        ant.taskdef(
                name: 'scanSuspendables', classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
                classpath: "build/classes/main:build/resources/main:${configurations.quasar.asPath}:${configurations.runtime.asPath}")
        ant.scanSuspendables(
                auto: false,
                suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
                supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
                append: true) {
            fileset(dir: sourceSets.main.output.classesDir)
        }
    }
}

protobuf {
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:2.6.1'
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}


sourceSets {
    main {
        java {
            srcDir 'src/generated/main'
        }
    }
}

version = 1.0
task dist(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'JPulseModule',
                'Implementation-Version': version,
                'Main-Class': 'net.digitalbebop.Main'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
